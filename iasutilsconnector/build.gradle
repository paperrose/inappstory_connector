apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def getVersionName = { ->
    return inappstory_connector_output_version
}

def getArtifactId = { ->
    return "iasutilsconnector"
}

def getGroupId = { ->
    return "com.github.inappstory"
}

def getGitHubUrl = { ->
    return "https://github.com/inappstory/android-sdk-utilsconnector"
}

android {
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35
        versionCode inappstory_connector_output_build
        versionName inappstory_connector_output_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            buildConfigField("int", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField("int", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }
}

configurations {
    filePickerConfig
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"


    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}

publishing {
    publications {
        bar(MavenPublication) {
            groupId getGroupId()
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/libs/${getArtifactId()}.jar")
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri(getGitHubUrl())
            credentials {
                username = project.findProperty("gpr.user") as String
                password = project.findProperty("gpr.key") as String
            }
        }
    }
}


project.afterEvaluate {
    def isAndroidLibraryProject = project.plugins.hasPlugin('com.android.library')
    if (isAndroidLibraryProject) {
        task copyLibs(type: Copy) {
            from configurations.filePickerConfig
            into "libs"
        }
        mergeReleaseJniLibFolders.dependsOn copyLibs

        task copyDeps(type: Copy) {
            from configurations.filePickerConfig {
                include '**/*.jar'
            }
            into "./build/intermediates/packaged-classes/release/libs"
            // this folder gets packaged inside the AAR
        }
        mergeReleaseJniLibFolders.dependsOn copyDeps
        // only this stage worked for me - neither earlier, nor later

        task copyDebugDeps(type: Copy) {
            from configurations.filePickerConfig {
                include '**/*.jar'
            }
            into "./build/intermediates/packaged-classes/debug/libs"
        }
        mergeDebugJniLibFolders.dependsOn copyDebugDeps
    }
}

